buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: "maven"
apply plugin: "signing"
apply plugin: "com.bmuschko.nexus"
apply plugin: "com.github.johnrengelman.shadow"

archivesBaseName = "bugvm-rt"
description = """BugVM Runtime Library"""

dependencies {
    compile fileTree(include: ["*.jar"], dir: "lib")
}

jar {
    manifest {
        attributes "Implementation-Version" : project.version
    }
    enabled = false
    dependsOn shadowJar
}

shadowJar {
    classifier = null
    exclude "META-INF/maven/"
    relocate "org.json", "com.bugvm.json"
    relocate "com.eaio", "com.bugvm.eaio"
}

task nativeLib(type:Exec) {
    workingDir "vm"
    commandLine "bash", "-c", ". build.sh --clean"
    doLast {
        copy {
            from("vm/binaries")
            into "bin"
        }
    }
}

modifyPom {
    project {
        name project.archivesBaseName
        description project.description
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution "repo"
            }
        }
        url "https://bugvm.com"

        scm {
            url "https://github.com/bugvm/bugvm"
            connection "scm:git:git://github.com/bugvm/bugvm.git"
            developerConnection "scm:git:ssh://github.com:bugvm/bugvm.git"
        }

        developers {
            developer {
                id "bugvm"
                name "We are the BugVM"
                email "bug@bugvm.com"
                organization "BugVM"
                organizationUrl "https://bugvm.com"
            }
        }
    }
}