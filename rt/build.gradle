buildscript {
     repositories {
          jcenter()
     }
     dependencies {
          classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
     }
}

apply plugin: "com.github.johnrengelman.shadow"

archivesBaseName = "bugvm-rt"
description = """BugVM Runtime Library"""

dependencies {
     compile "commons-io:commons-io:2.4"
}

jar {
     manifest {
          attributes "Implementation-Version" : project.version
     }
     enabled = false
}

shadowJar {
     classifier = null
     relocate 'org.bouncycastle', 'com.android.org.bouncycastle'
     relocate 'org.conscrypt', 'com.android.org.conscrypt'
     relocate 'com.squareup', 'com.android.com.squareup'
}

jar.dependsOn shadowJar

task nativeLib(type:Exec) {
     workingDir 'vm'

     commandLine 'bash', '-c', '. build.sh --clean --build=release'

}

publishing {
     publications {
          mavenJava(MavenPublication) {
               artifactId project.archivesBaseName

               from components.java
          }
     }
     repositories {
          maven {
               url mavenUrlSnapshot
               credentials {
                    username = mavenUsername
                    password = mavenPassword
               }
          }
     }
}

uploadArchives {
     repositories {
          mavenDeployer {

               snapshotRepository(url: mavenUrlSnapshot) {
                    authentication(userName: mavenUsername, password: mavenPassword)
               }

               pom.project {
                    name project.archivesBaseName
                    description project.description
                    url 'https://bugvm.com'

                    scm {
                         url 'https://github.com/bugvm/bugvm.git'
                         connection 'scm:git:git://github.com/bugvm/bugvm.git'
                    }

                    developers {
                         developer {
                              id 'bugvm'
                              name 'The Bug'
                              email 'bug@bugvm.com'
                              url 'https://bugvm.com'
                         }
                    }
               }
          }
     }
}